plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.compose'
    id 'maven-publish'
}

android {
    namespace 'com.stevdzasan.onetap'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
    }
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    // Compose
    implementation 'androidx.activity:activity-compose:1.9.0'
    implementation("androidx.compose.material3:material3:1.2.1")
    debugImplementation("androidx.compose.ui:ui-tooling:1.6.8")
    implementation("androidx.compose.ui:ui-tooling-preview:1.6.8")

    // Contains the core Credential Manager functionalities including password
    // and passkey support.
    implementation("androidx.credentials:credentials:1.3.0-rc01")
    // Provides support from Google Play services for Credential Manager,
    // which lets you use the APIs on older devices.
    implementation("androidx.credentials:credentials-play-services-auth:1.3.0-rc01")
    implementation("com.google.android.libraries.identity.googleid:googleid:1.1.1")

    // Google Auth
    implementation("com.google.android.gms:play-services-auth:21.2.0")

    // JWT Decoder
    implementation("com.auth0.android:jwtdecode:2.0.2")

    // Gson needed for Proguard rules for JWT Decoder library (For now).
    // Because JWT Decoder doesn't add those Proguard rules by default.
    implementation("com.google.code.gson:gson:2.10.1")
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.stevdza-san'
            artifactId = 'OneTapCompose'
            version = '1.0.14'

            afterEvaluate {
                from components.release
            }
        }
    }
}
